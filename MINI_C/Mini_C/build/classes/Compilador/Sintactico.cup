package Compilador;
import java_cup.runtime.*;
import java.util.*;

parser code {:
    
    public String listOfErrors ="";
    public String bitacora = "";
    public void syntax_error(Symbol s){
		report_error("Error de sintaxis. Línea: " + (s.right + 1) +
		" Columna: " + (s.left +1) + ". Texto: \"" + s.value + "\"", null);
            listOfErrors+="Línea: " + (s.right + 1) +
		" Columna: " + s.left + " Error de sintaxis. Texto: \"" + s.value + "\""+"\r\n";
	}
        public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception 
        {
         //report_fatal_error("Fatal Error: " + s.value, null);
        }
:};

action code {:
      Tabla table = new Tabla();
      public String MiContexto = "Global";
      boolean SoyParametro = false;
      String TipoStmt = "noName";
:};

terminal Symbol     semicolon, intConstantType, doubleConstantType, boolConstantType, stringConstantType, lBracket, rBracket, lParenthesis, rParenthesis, VOID,
                    comma, CLASS, EXTENDS, lBrace, rBrace, IMPLEMENTS, INTERFACE, IF, ELSE, FOR, WHILE, RETURN,
                    BREAK, PRINT, EQUAL, THIS, PLUS, MINUS, TIMES, DIV, MOD, LESST, LESSTE, MORET, MORETE, EQUALTO,
                    NOEQUAL, AND, OR, NOT, NEW_KW, NEWARRAY_KW, READINT_KW, READLINE_KW, MALLOC_KW, point, GETBYTE_M,
                    SETBYTE_M, NULL, BRACKETS;

terminal (Symbol)    ident, stringConstants;
terminal (Symbol)    integerConstants;
terminal (Symbol)    doubleConstants;
terminal (Symbol)    booleanConstants;

non terminal Symbol Program, Decl, VariableDecl, Variable, Type, FunctionDecl, Formals, ClassDecl, Field, 
                    InterfaceDecl, Prototype, StmtBlock, Stmt, IfStmt, WhileStmt, ForStmt, ReturnStmt, 
                    BreakStmt, PrintStmt, Expr, LValue, Call, LibCall, Actuals, Constant;

//no terminals maded by me

non terminal Symbol DeclLR, FormalsLR, ClassOptional, ClassOptional2, ClassOptional3, ClassClosing,
                    InterfaceClosing, VariableDeclClosing, StmtClosing, ExprOptional, ExprLR, IfStmtElse;

precedence left EQUAL;
precedence left OR, AND;
precedence nonassoc LESST, LESSTE, MORET, MORETE, EQUALTO, NOEQUAL;
precedence left PLUS, MINUS;
precedence left TIMES, DIV, MOD;
precedence nonassoc NOT;
precedence nonassoc lBracket, point;
precedence nonassoc ELSE;



Program             ::= Decl DeclLR;
DeclLR              ::= Decl DeclLR 
                    |;
Decl                ::= VariableDecl
                    | FunctionDecl
                    | ClassDecl 
                    | InterfaceDecl
                    | error semicolon
                    | error rBrace;

VariableDecl        ::= Variable semicolon;
Variable            ::= Type:t ident:i {:
                        String val = null;
                        if(SoyParametro)
                                    table.Insertar(new Element( i.toString(),String.valueOf(t),val,MiContexto,"Parametro"));
                              else
                                    table.Insertar(new Element( i.toString(),String.valueOf(t),val,MiContexto,"Variable"));
                        :};
Type                ::= intConstantType:t {:RESULT = t;:}
                    | doubleConstantType:t {:RESULT = t;:}
                    | boolConstantType:t {:RESULT = t;:}
                    | stringConstantType:t {:RESULT = t;:}
                    | ident:t {:RESULT = t;:}
                    | Type BRACKETS;
FunctionDecl        ::= Type:t ident:i {:
                        table.Insertar(new Element( i.toString(),String.valueOf(t),"null",MiContexto,"Función"));
                        MiContexto += "." + i;
                        :}lParenthesis {:SoyParametro = true;:} Formals rParenthesis {:SoyParametro = false;:} StmtBlock {:MiContexto = table.RetirarContexto(MiContexto); :}
                    | VOID:t ident:i {:
                        table.Insertar(new Element( i.toString(),String.valueOf(t),"null",MiContexto,"Procedimiento"));
                        MiContexto += "." + i;
                        :} lParenthesis {:SoyParametro = true;:}  Formals {:SoyParametro = false;:} rParenthesis StmtBlock {:MiContexto = table.RetirarContexto(MiContexto); :};
Formals             ::= Variable FormalsLR |;
FormalsLR           ::= comma Variable FormalsLR
                    |;
ClassDecl           ::= CLASS:t ident:i {:
                         table.Insertar(new Element( i.toString(),String.valueOf(t),"null",MiContexto,"Clase"));
                         MiContexto += "." + i;
                        :} ClassOptional ClassOptional2 lBrace ClassClosing rBrace {: MiContexto = table.RetirarContexto(MiContexto); :};
ClassOptional       ::= EXTENDS ident
                    |;
ClassOptional2      ::= IMPLEMENTS ident ClassOptional3
                    |;
ClassOptional3      ::= comma ident ClassOptional3
                    |;
ClassClosing        ::= Field ClassClosing
                    |;
Field               ::= VariableDecl
                    | FunctionDecl;
InterfaceDecl       ::= INTERFACE:t ident:i {:
                        table.Insertar(new Element( i.toString(),String.valueOf(t),"null",MiContexto,"Interface"));
                        MiContexto += "."+ i;
                        :} lBrace InterfaceClosing rBrace {: MiContexto = table.RetirarContexto(MiContexto);:};
InterfaceClosing    ::= Prototype InterfaceClosing
                    |;
Prototype           ::= Type ident lParenthesis Formals rParenthesis semicolon
                    | VOID  ident   lParenthesis Formals rParenthesis semicolon;
StmtBlock           ::= lBrace VariableDeclClosing StmtClosing rBrace;
VariableDeclClosing ::= VariableDeclClosing VariableDecl 
                    |;
StmtClosing         ::= Stmt StmtClosing
                    |;
Stmt                ::= ExprOptional semicolon
                    | IfStmt 
                    | WhileStmt 
                    | ForStmt 
                    | BreakStmt 
                    | ReturnStmt 
                    | PrintStmt
                    | StmtBlock;
ExprOptional        ::= Expr
                    |;
IfStmt              ::= IF lParenthesis Expr rParenthesis Stmt IfStmtElse;
IfStmtElse          ::=  ELSE Stmt
                    |;
WhileStmt           ::= WHILE lParenthesis Expr rParenthesis Stmt;
ForStmt             ::= FOR lParenthesis ExprOptional semicolon Expr semicolon ExprOptional rParenthesis Stmt;
ReturnStmt          ::= RETURN ExprOptional semicolon;
BreakStmt           ::= BREAK semicolon;
PrintStmt           ::= PRINT lParenthesis Expr ExprLR rParenthesis semicolon;
ExprLR              ::= comma Expr ExprLR
                    |;
Expr                ::= LValue EQUAL Expr:e1 {:RESULT = e1;:}
                    | Constant:c {:RESULT = c;:}
                    | LValue
                    | THIS
                    | Call
                    | lParenthesis Expr rParenthesis
                    | Expr:e1 PLUS Expr:e2 {: if(e1 != null && e2 != null){
                                                       RESULT = (Symbol)((Object)(Double.parseDouble(String.valueOf(e1)) + Double.parseDouble((String.valueOf(e2)))));
                                                        System.out.println(String.valueOf(RESULT)); 
                                                }                              
                                              :}
                    | Expr:e1 MINUS Expr:e2 {: if(e1 != null && e2 != null){
                                                       RESULT = (Symbol)((Object)(Double.parseDouble(String.valueOf(e1)) - Double.parseDouble((String.valueOf(e2)))));
                                                        System.out.println(String.valueOf(RESULT)); 
                                                }                              
                                            :} 
                    | Expr TIMES Expr
                    | Expr DIV Expr
                    | Expr MOD Expr
                    | MINUS Expr
                    | Expr LESST Expr
                    | Expr LESSTE Expr
                    | Expr MORET Expr
                    | Expr MORETE Expr
                    | Expr EQUALTO Expr
                    | Expr NOEQUAL Expr
                    | Expr AND Expr
                    | Expr OR Expr
                    | NOT Expr
                    | NEW_KW lParenthesis ident rParenthesis
                    | NEWARRAY_KW lParenthesis Expr comma Type rParenthesis
                    | READINT_KW lParenthesis rParenthesis
                    | READLINE_KW lParenthesis rParenthesis
                    | MALLOC_KW lParenthesis Expr rParenthesis;
LValue              ::= ident:i {:RESULT = i;:}
                    | Expr point ident 
                    | Expr lBracket Expr rBracket;

Call                ::= ident lParenthesis Actuals rParenthesis
                    | Expr point ident lParenthesis Actuals rParenthesis
                    | Expr point LibCall lParenthesis Actuals rParenthesis;
LibCall             ::= GETBYTE_M lParenthesis Expr rParenthesis
                    | SETBYTE_M lParenthesis Expr comma Expr rParenthesis;
Actuals             ::= Expr ExprLR
                    |;
Constant            ::= integerConstants:i {:RESULT = i;:}
                    | doubleConstants:i {:RESULT = i;:}
                    | booleanConstants:i {:RESULT = i;:}
                    | stringConstants:i {:RESULT = i;:}
                    | NULL:i {:RESULT = i;:};
                






